buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.6.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.51.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.15.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.19.0'
        classpath 'com.palantir.gradle.failure-reports:gradle-failure-reports:1.13.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.50.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.27.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:5.69.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.1.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.7.0'
        classpath 'com.gradle.publish:plugin-publish-plugin:1.3.0'
    }
}
allprojects {
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'com.palantir.jakarta-package-alignment'
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.external-publish-gradle-plugin'
apply plugin: 'com.palantir.failure-reports'
// So we get publishToMavenLocal
apply plugin: 'maven-publish'

repositories {
    mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    gradlePluginPortal()
}

apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.jdks'

group 'com.palantir.gradle.externalpublish'
version System.env.CIRCLE_TAG ?: gitVersion()

apply plugin: 'com.palantir.baseline'
apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'org.inferred.processors'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

dependencies {
    implementation 'io.github.gradle-nexus:publish-plugin'
    implementation 'com.netflix.nebula:nebula-publishing-plugin'
    implementation 'com.netflix.nebula:gradle-info-plugin'
    implementation 'com.palantir.gradle.utils:environment-variables'
    implementation 'org.jetbrains.intellij.plugins:gradle-intellij-plugin'

    testImplementation gradleTestKit()
    testImplementation 'com.google.guava:guava'
    testImplementation 'com.netflix.nebula:nebula-test'
    testImplementation 'org.rauschig:jarchivelib'
}


gradlePlugin {
    website = 'https://github.com/palantir/gradle-external-publish-plugin'
    vcsUrl = 'https://github.com/palantir/gradle-external-publish-plugin'

    plugins {
        def pluginDescription = { artifactType ->
            "Publishes ${artifactType} from a Palantir open source project to an open source package repository"
        }

        externalPublish {
            id = 'com.palantir.external-publish'
            displayName = 'Palantir External Publish Plugin'
            description = pluginDescription('artifacts')
            implementationClass = 'com.palantir.gradle.externalpublish.ExternalPublishRootPlugin'
            tags.set(['publishing'])
        }
        externalPublishJar {
            id = 'com.palantir.external-publish-jar'
            displayName = 'Palantir External Publish Jar Plugin'
            description = pluginDescription('jars')
            implementationClass = 'com.palantir.gradle.externalpublish.ExternalPublishJarPlugin'
            tags.set(['publishing'])
        }
        externalPublishDist {
            id = 'com.palantir.external-publish-dist'
            displayName = 'Palantir External Publish Dist Plugin'
            description = pluginDescription('distributions')
            implementationClass = 'com.palantir.gradle.externalpublish.ExternalPublishDistPlugin'
            tags.set(['publishing'])
        }
        externalPublishApplicationDist {
            id = 'com.palantir.external-publish-application-dist'
            displayName = 'Palantir External Publish Application Dist Plugin'
            description = pluginDescription('application distributions')
            implementationClass = 'com.palantir.gradle.externalpublish.ExternalPublishApplicationDistPlugin'
            tags.set(['publishing'])
        }
        externalPublishConjure {
            id = 'com.palantir.external-publish-conjure'
            displayName = 'Palantir External Publish Conjure Plugin'
            description = pluginDescription('conjure IR')
            implementationClass = 'com.palantir.gradle.externalpublish.ExternalPublishConjurePlugin'
            tags.set(['publishing'])
        }
        externalPublishIntellij {
            id = 'com.palantir.external-publish-intellij'
            displayName = 'Palantir External Publish Intellij Plugin'
            description = pluginDescription('an intellij plugin')
            implementationClass = 'com.palantir.gradle.externalpublish.ExternalPublishIntellijPlugin'
            tags.set(['publishing'])
        }
        externalPublishCustom {
            id = 'com.palantir.external-publish-custom'
            displayName = 'Palantir External Publish Custom Plugin'
            description = pluginDescription('custom gradle publications')
            implementationClass = 'com.palantir.gradle.externalpublish.ExternalPublishCustomPlugin'
            tags.set(['publishing'])
        }
    }
}

tasks.named('processResources').configure {
    duplicatesStrategy 'include'
}

javaVersions {
    libraryTarget = 17
}

tasks.named('test') {
    systemProperty 'ignoreDeprecations', 'true'
}

jdks {
    daemonTarget = 17
}

